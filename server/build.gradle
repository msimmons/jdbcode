apply plugin: net.contrapt.kt2ts.Kt2TsPlugin

dependencies {
    compile group: 'com.zaxxer', name: 'HikariCP', version: hikari_version
    compile project(":grammar")

    compileOnly "io.vertx:vertx-core:$vertx_version"
    compileOnly "io.vertx:vertx-web:$vertx_version"
    runtime "ch.qos.logback:logback-classic:1.2.3"

    testCompile "io.vertx:vertx-unit:$vertx_version"
    testCompile "com.h2database:h2:1.4.197"
}

jar {
    manifest {
        attributes 'Main-Class': 'net.contrapt.jdbcode.Application'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task deploy(dependsOn: ['deployJar', 'deployTs']) {}

task deployJar(type: Copy, dependsOn: ['build']) {
    from ("${project.buildDir}/libs") {
        include jar.archiveName
        rename jar.archiveName, 'jdbcode.jar'
    }
    into "${project.rootDir}/out"
}

task deployTs(type: Copy, dependsOn: ['kt2ts']) {
    // Typescript defs
    from ("${project.buildDir}/kt2ts") {
        include "models.d.ts"
        rename "models.d.ts", "index.d.ts"
    }
    into "${project.rootDir}/node_modules/server-models"
}

k2ts {
    jarFile = "${project.buildDir}/libs/${jar.archiveName}"
    packages += [
          "net.contrapt.jdbcode.model"
    ]
    outFile = "models.d.ts"
    moduleName = "server-models"
}
kt2ts.dependsOn = ['jar']
